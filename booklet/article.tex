\documentclass[a4paper,11pt]{article}

\usepackage{inputenc}
\usepackage{euler}
\usepackage[T1]{fontenc}
\usepackage{libertine}
\usepackage{lipsum}
\usepackage{fancyvrb}
\usepackage{url}
\usepackage[english]{babel}
\usepackage{amsmath}
\usepackage{amsthm}
\usepackage{amssymb}
\usepackage{acronym}
\usepackage{hyperref}
\usepackage{tabu}
\usepackage{rotating}
\usepackage{mathdots}
\usepackage{minted}
\usepackage{units}
\usepackage{float}
\usepackage{bbold}
\usepackage[sort&compress,square,comma,authoryear]{natbib}

\fvset{fontsize=\normalsize}
\setmonofont[Scale=0.8]{Menlo}
\usemintedstyle{xcode}
\hypersetup{colorlinks=false, citecolor=Violet}

\newtheorem{theorem}{Theorem}
\newtheorem{lemma}[theorem]{Lemma}
\newtheorem{proposition}[theorem]{Proposition}
\newtheorem{corollary}[theorem]{Corollary}
\newtheorem{definition}[theorem]{Definition}
\newtheorem{remark}[theorem]{Remark}
\newtheorem{example}[theorem]{Example}

\newcommand{\stcode}[1]{\mintinline[fontsize=\footnotesize]{smalltalk}{#1}}

% *********************************************************************
% Revisions and comments Macros
% *********************************************************************
\usepackage{xcolor}
\usepackage{xspace}
\usepackage[normalem]{ulem} 			% \sout macro
\newboolean{showcomments}
% \setboolean{showcomments}{false}
\setboolean{showcomments}{true}

\ifthenelse{\boolean{showcomments}}
{
	\newcommand{\nb}[3]{
		{\colorbox{#2}{\bfseries\sffamily\scriptsize\textcolor{white}{#1}}}
		{\textcolor{#2}{\sf\small$\blacktriangleright$\textit{#3}$\blacktriangleleft$}}}
	\newcommand{\bnote}[2]{\fbox{\color{blue}\bfseries\sffamily\scriptsize#1}
    	{\color{blue}\sf\small$\blacktriangleright$\textit{#2}$\blacktriangleleft$}}
	\newcommand{\old}[1]{{\color{gray}\sout{#1}}} % old to be removed
	\newcommand{\del}[1]{\old{#1}} % please remove
	\newcommand{\ins}[1]{{\textcolor{blue}{\uline{#1}}}} % please insert
	\newcommand{\ugh}[1]{{\textcolor{red}{\uwave{#1}}}} % please rephrase
	\newcommand{\chg}[2]{{\textcolor{red}{\sout{#1}}}{\ra}\textcolor{blue}{\uline{#2}}} % please change
	\newcommand{\here}{\bnote{***}{CONTINUE HERE}}
	\newcommand{\fix}[1]{\bnote{FIX}{#1}}
}{
	\newcommand{\bnote}[2]{}
	\newcommand{\nb}[3]{}
	\newcommand{\old}[1]{}
	\newcommand{\del}[1]{}
	\newcommand{\ins}[1]{}
	\newcommand{\ugh}[1]{}
	\newcommand{\chg}[2]{}
	\newcommand{\here}{}
	\newcommand{\fix}[1]{}
}
%---% add your own macros
\newcommand{\sd}[1]{\nb{Stef}{orange}{#1}}
\newcommand{\mn}[1]{\nb{Massimo}{blue}{#1}}

\newcommand{\ct}[1]{{\textsf{#1}}\xspace}

\author{Massimo Nocentini}
\title{Relational programming in Smalltalk}

\begin{document}

\maketitle

\begin{abstract}
This booklet is an extended description of $\mu$Kanren, a relational
interpreter formerly implemented in Scheme and ported in Smalltalk for the sake
of understanding and, of course, fun. It enjoys simplicity and elegance despite
the complex nature of logic systems; moreover, using an uniform but powerful
language such as Smalltalk it is possible to appreciate and benefit from both
properties.
\end{abstract}

\section*{Introduction}

\input{introduction.tex}

\tableofcontents

\section{Preparing the wires...}

\sd{As a style point of view I would not put code inside sentence. We should be
able to read the text without the code and the code without the text. It makes
paragraphs more self contained.}

\sd{Why don't you start with Cons. Then once people understand Cons and infinite stream
you can introduce Goal and state and always example first.
\mn{I do prefer to explain both of them when the need arises to not postpone the main definition of Goal.}}

\sd{Having an diagram showing how an object will be composed will also help}

In this section we describe the fundamentals of $\mu$Kanren and the protocols
that objects provide to compose the architecture on which Section
\ref{sec:bootstrapping} will define core logic operators.

We start describing objects that (i)~encodes relational statements,
(ii)~logical variables and (iii)~associations of values to variables such that
a given predicate can be satisfied.

\subsection{Goals, variables and states}

A \textit{goal} $g$ denotes a logical relation; precisely, $g$ is any object
that responds to the message \ct{\#onState:} so that it receives a substitution
and produces an ordered sequence of zero or more substitutions.

In turn, a substitution is an unordered collection of
values which are indexed by \textit{logic variables}, therefore a
\ct{Dictionary} object fits perfectly. For the sake of clarity, the predicate
\begin{displaymath}
inc(X, succ(succ(zero))) \quad\text{where}\quad Y = succ(X) \rightarrow inc(X, Y),
\end{displaymath}
is satisfied by the substitution
\begin{minted}[fontsize=\footnotesize]{smalltalk}
|X|
X := (Var id: 0).
Dictionary new
    at: X put: 'succ(zero)';
    yourself
\end{minted}

Moreover, logic variables are created by means of the message
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Var class >> id: anInteger
    ^ self new
        id: ('var_' , anInteger asString) asSymbol;
        yourself
\end{minted}

We wrap a substitution in a \ct{State} object that keeps record of metadata
about the computation, for instance a \ct{State} has an integer \ct{birthdate}
to track the (discrete) time when a logic variable has been introduced -- its
meaning will be clarified later when we'll describe the \ct{Fresh}
goal\footnote{put here the reference to its subsection}.

An initial \ct{State} object is created by
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Dictionary new asState
\end{minted}
where
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Dictionary >> asState
    ^ State new
        birthdate: 0;
        substitution: self;
        yourself
\end{minted}
and it can be augmented by
\begin{minted}[fontsize=\footnotesize]{smalltalk}
State >> at: aVar put: aValue
    | s |
    s := substitution copy.
    "check that if a key already exists at aVar, then the values must be the same"
    s
      at: aVar
      ifPresent: [:v | aValue = v ifFalse: [UnificationError signal]]
      ifAbsentPut: aValue.
    ^ self class new
        birthdate: birthdate;
        substitution: s;
        yourself
\end{minted}
maintaining a coherent protocol with respect to the one of \Verb|Dictionary|
objects for extension; finally, \ct{UnificationError} objects will be
explained in a later section when will talk about the unification process.

\subsection{Cons cells}

Because the sequence of substitutions may be infinite, we represent it not as a
\ct{LinkedList} object but as a \ct{Chain} object which has the capability to
represent potentially infinite sequences.  For this we will start building
\ct{Cons} cells; following the Lisp tradition, we reproduce the usual behavior
as the next test method shows:
\begin{minted}[fontsize=\footnotesize]{smalltalk}
testCons
    self
        assert: '(0)'
        equals: (0 cons: nil) asString.
    self
        assert: '(0 1 2 3 4 5 6 7 8 9)'
        equals: (0 to: 9) asCons asString.
    self
        assert: '((1 2) 3 4 (5 (6 8)) 7)'
        equals: {{1. 2}. 3. 4. {5. {6. 8}}. 7} asCons asString.

    "dotted pairs notation"
    self
        assert: '(0 . 1)'
        equals: (0 cons: 1) asString.
    self
        assert: '(0 1 2 3 4 5 6 7 8 9 . 10)'
        equals: ((0 to: 9) asConsWithCdr: 10) asString.
    self
        assert: '(((3 . 4) . 5) 6 7)'
        equals: (((3 cons: 4) cons: 5) cons: (6 cons: (7 cons: nil))) asString
\end{minted}
We split its description of
\begin{itemize}
\item the creation message \ct{\#cons:} is defined as
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Object >> cons: anObj
    ^ anObj consedObject: self
\end{minted}
which dispatches the message \ct{\#consedObject:} on its argument to build
\ct{Cons} cells with
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Object >> consedObject: car
    ^ Cons car: car cdr: self

Cons class >>  car: anObj cdr: anotherObj
    ^ self new
        car: anObj;
        cdr: anotherObj;
        yourself
\end{minted}

\item the translation message \ct{\#asCons} is defined and overridden as
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Object >> asCons
    ^ Cons fromObject: self

ByteSymbol >> asCons
    ^ Cons fromByteSymbol: self

SequenceableCollection >> asCons
    ^ Cons fromSequenceableCollection: self
\end{minted}
in parallel, the class object \ct{Cons} responds as follows
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Cons >> fromObject: anObj
    ^ anObj

Cons >> fromByteSymbol: aSymbol
    ^ aSymbol

Cons >> fromSequenceableCollection: aCollection
    ^ self fromSequenceableCollection: aCollection lastCdr: nil

Cons >> fromSequenceableCollection: aCollection lastCdr: aCdr
    ^ aCollection
        foldr: [:each :acc | self car: each asCons cdr: acc asCons]
        init: aCdr
\end{minted}
Finally, to build the more general notion of \textit{dotted pairs},
the message \ct{\#asConsWithCdr:} is defined and overridden as
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Object >> asConsWithCdr: aCdr
    ^ Cons fromObject: self lastCdr: aCdr

SequenceableCollection >> asConsWithCdr: aCdr
    ^ Cons fromSequenceableCollection: self lastCdr: aCdr
\end{minted}
which require
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Cons >> fromObject: anObj lastCdr: aCdr
    ^ self car: anObj cdr: aCdr
\end{minted}

\end{itemize}

\subsection{Chains}

We find \ct{BlockClosure} objects good candidates to
represent a suspended (and possibly infinite) computation. The following
auxiliary methods for the test case \ct{ChainTest} show examples of how
\ct{BlockClosure}s can be used as arguments for the message \ct{\#cons:},
\begin{minted}[fontsize=\footnotesize]{smalltalk}
ChainTest >> collatz: o
    ^ o
        cons: [o even
                ifTrue: [self collatz: o / 2]
                ifFalse: [self collatz: 3 * o + 1]]

ChainTest >> fibs
    ^ self fib: 0 fib: 1

ChainTest >> fib: m fib: n
    ^ m cons: [self fib: n fib: m + n]

ChainTest >> gcd: m gcd: n
    ^ m cons: [n isZero
                ifTrue: [Chain with: m]
                ifFalse: [self gcd: n gcd: m \\ n]]

ChainTest >> nats
    ^ self ints: 0

ChainTest >> ints: i
    ^ i cons: [self ints: i + 1]

ChainTest >> zeroOneTwo
    ^ 0 cons: (1 cons: (2 cons: Chain bottom))
\end{minted}
In turn, when a \ct{BlockClosure} receives the dispatched message
\begin{minted}[fontsize=\footnotesize]{smalltalk}
BlockClosure >> consedObject: car
    ^ Chain item: car linker: self
\end{minted}
then it builds a \ct{Chain}; precisely, \ct{Chain} is an abstract class with
two subclasses \ct{Bottom} which denotes an empty sequence and \ct{Knot} which
denotes a sequence that has a value and a tail sequence that is not evaluated
yet, providing a \ct{BlockClosure} to do that, one item at a time.
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Chain class >> bottom
    ^ Bottom new

Chain class >> item: anObj linker: aBlockClosure
    ^ Knot new
        item: anObj;
        linker: aBlockClosure;
        yourself
\end{minted}
Moreover, a \ct{Chain} can be used as argument of a \ct{\#cons:} message send
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Chain >> consedObject: car
    ^ car cons: [self]
\end{minted}
directly and other constructors are provided by \ct{Chain} class messages
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Chain class >> with: anObj
    ^ anObj cons: Chain bottom

Chain class >> repeat: anObj
    ^ anObj cons: [self repeat: anObj]
\end{minted}
that allow us to write the following comprehensive test case
\begin{minted}[fontsize=\footnotesize]{smalltalk}
ChainTest >> testNumbers
    self
        assert: Chain bottom contents
        equals: LinkedList new.
    self
        assert: (self nats next: 10) contents
        equals: (0 to: 9).
    self
        assert: (self zeroOneTwo next: 10) contents
        equals: (0 to: 2).
    self
        assert: (self zeroOneTwo next: 1) contents
        equals: (0 to: 0).
    self
        assert: (self fibs next: 10) contents
        equals: {0. 1. 1. 2. 3. 5. 8. 13. 21. 34}.
    self
        assert: ((self collatz: 10) next: 10) contents
        equals: {10. 5. 16. 8. 4. 2. 1. 4. 2. 1}.
    self
        assert: ((self gcd: 18 gcd: 32) next: 10) contents
        equals: {18. 32. 18. 14. 4. 2. 2}.
    self
        assert: ((Chain repeat: 4) next: 10) contents
        equals: {4. 4. 4. 4. 4. 4. 4. 4. 4. 4}
\end{minted}

On one hand, when a \ct{Chain} object receives the message
\ct{next:} that carries a \ct{SmallInteger} object $n$ then it keeps at most
$n$ objects and then truncate itself, precisely
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Bottom >> next: n
    ^ self

Knot >> next: n
    ^ n = 1
        ifTrue: [Chain with: item]
        ifFalse: [item cons: [self next next: n - 1]]
\end{minted}

On the other hand, when a \ct{Chain} object receives the message
\ct{\#contents} it builds a \ct{LinkedList} object \textit{eagerly},
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Bottom >> contents
    ^ LinkedList new

Knot >> contents
    ^ self next contents
        addFirst: item;
        yourself
\end{minted}
where \Verb|Knot| objects provide the following simple but fundamental message
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Knot >> next
    ^ linker value
\end{minted}
that allows us to do one more step in filling the not yet evaluated rest of the
sequence. Observe that the \ct{Chain} hierarchy supports the protocol provided
by \ct{ReadStream} objects because of the messages \ct{\#next}, \ct{\#next:}
and \ct{\#contents}.

\subsection{The Runner}

We can now be more precise and rephrase that a \ct{Goal} object responds with a
\ct{Chain} that contains either zero, one or more substitutions when it
receives the message \ct{\#onState:}.

In this section we describe three components that have the responsibility to
report and to present the substitutions that satisfy the given goal. The entry
point is the message
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Goal >> solutions
    ^ Runner onGoal: self

Runner class >> onGoal: aGoal
    ^ self new
        goal: aGoal;
        yourself
\end{minted}
that allows a goal to build a \ct{Runner} which has the responsibilities to
process the \ct{Chain} of substitutions; in particular, (i)~all of them with
the message
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Runner >> all
    ^ self run contents
\end{minted}
and (ii)~at most $n$ of them with the message
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Runner >> atMost: anInteger
    ^ (self run next: anInteger) contents
\end{minted}

Those messages rely on the unary message
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Runner >> run
    | states vars selector |
    states := goal onState: Dictionary new asState.
    vars := goal vars ifEmpty: [LinkedList with: Var tautology].
    selector := vars size = 1 ifTrue: #first ifFalse: #yourself.
    ^ states
        collect: [:s | selector
                        value: (s reifier reifyVars: vars)]
\end{minted}
that accomplishes the responsibilities
\begin{itemize}

\item to send the message \Verb|#onState:| to a \Verb|Goal| $g$ in order to
start the resolution process from an empty state -- this can be considered the
entry point of the ``computation'' that produces the \Verb|Chain| of
substitutions that satisfy $g$;

\item to take into account the variables with respect to results should be
provided; as a particular case, if the root \ct{Goal} $g$ doesn't introduce any logic
variable, recovered by the messages
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Goal >> vars
    ^ {}

Fresh >> vars
    ^ vars
\end{minted}
then it is equivalent to prove that $g$ is a \textit{tautology}, represented
by
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Var class >> tautology
    ^ #tautology
\end{minted}
which requires to decide that a relation is always \textit{true} regardless
the values associated to logic variables;

\item to simplify the presentation of results when the root \Verb|Goal| object
introduces \textit{exactly one} logic variable $v$: in this case a
\Verb|LinkedList| of values associated to $v$ will be produced; otherwise, a
\Verb|LinkedList| of \Verb|LinkedList| of values will be produced, a list for
each tuple of logic variables introduced -- this point will become clear when
we'll explain \Verb|Fresh| goals that are the only ones able to introduce logic
variables;

\item to map each substitution (always translate to \Verb|State| object) that
satisfies the root \Verb|Goal| object to a \textit{reified} substitution.
\end{itemize}

The last point shows that \Verb|Chain| objects are \textit{functors} according
to the Haskell terminology because they answer to the message \Verb|#collect:|,
which is a synonism for the function \Verb|fmap :: (a -> b) -> f a -> f b|, as
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Chain >> collect: aBlockClosure
    ^ self

Knot >> collect: aBlockClosure
    | v |
    v := aBlockClosure value: item.
    ^ v cons: [self next collect: aBlockClosure]
\end{minted}
respectively.

\subsection{The Reifier}

We are now ready to describe the reification process which
requires
\begin{minted}[fontsize=\footnotesize]{smalltalk}
State >> reifier
    ^ Reifier on: self

Reifier >> reifyVars: vars
    | walker reifier |
    walker := state walker.
    reifier := Dictionary new asState reifier.
    ^ vars
        collect: [:v |
                    | w s |
                    w := walker value: v.
                    s := reifier value: w.
                    s walker value: w]
\end{minted}
which uses the message
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Reifier >> value: anObj
    ^ (state walk: anObj)
        reifyUsingReifier: self
\end{minted}
to dispatch the message \Verb|#reifyUsingReifier:| over
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Object >> reifyUsingReifier: aReifier
    ^ aReifier reifyObject: self

Var >> reifyUsingReifier: aReifier
    ^ aReifier forVar: self

ReifiedVar >> reifyUsingReifier: aReifier
    ^ aReifier forReifiedVar: self

Array >> reifyUsingReifier: aReifier
    ^ aReifier forArray: self

LinkedList >> reifyUsingReifier: aReifier
    ^ aReifier forLinkedList: self

Cons >> reifyUsingReifier: aReifier
    ^ aReifier forCons: self
\end{minted}
and to collect replies as
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Reifier >> forObject: anObj
    ^ state

Reifier >> forVar: aVar
    ^ state reifyVar: aVar

Reifier >> forReifiedVar: aVar
    ^ state

Reifier >> forArray: aCollection
    ^ self _forCollection: aCollection

Reifier >> forLinkedList: aCollection
    ^ self _forCollection: aCollection

Reifier >> forCons: aCollection
    ^ self _forCollection: aCollection

Reifier >> _forCollection: aCollection
    ^ aCollection inject: state into: [:s :c | s reifier value: c]
\end{minted}
requiring the auxiliary message
\begin{minted}[fontsize=\footnotesize]{smalltalk}
State >> reifyVar: aVar
    substitution
        at: aVar
        ifAbsentPut: [ReifiedVar new
                        id: substitution size;
                        yourself]
\end{minted}
and
\begin{minted}[fontsize=\footnotesize]{smalltalk}
State >> walk: anObj
    | k |
    k := anObj.
    [k := substitution at: k ifAbsent: [^ k]] repeat
\end{minted}
Observe that \ct{Chain} objects are also \textit{foldable} according to the
Haskell \ct{Foldable} typeclass because they respond to the message
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Cons >> inject: init into: aBlock
    | accumulated |
    accumulated := aBlock value: init value: car.
    ^ aBlock value: accumulated value: cdr
\end{minted}

\subsection{The Walker}
The reification process requires \ct{Walker} objects which are built by
\ct{State} objects using the template method
\begin{minted}[fontsize=\footnotesize]{smalltalk}
State >> walker
    ^ Walker on: self
\end{minted}
and it responds to the message
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Walker >> value: aVar
    ^ (state walk: aVar)
        walkStarUsingWalker: self
\end{minted}
and objects of different classes responds to \Verb|#walkStarUsingWalker:| as follows
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Object >> walkStarUsingWalker: aWalker
    ^ aWalker forObject: self

Array >> walkStarUsingWalker: aWalker
    ^ aWalker forArray: self

LinkedList >> walkStarUsingWalker: aWalker
    ^ aWalker forLinkedList: self

Cons >> walkStarUsingWalker: aWalker
    ^ aWalker forCons: self
\end{minted}
dispatching over \Verb|Walker| objects
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Walker >> forObject: anObj
    ^ state walk: anObj

Walker >> forArray: aCollection
    ^ self _forCollection: aCollection

Walker >> forLinkedList: aCollection
    ^ self _forCollection: aCollection

Walker >> forCons: aCons
    ^ self _forCollection: aCons

Walker >> _forCollection: aCollection
    ^ aCollection
        collect: [:each | self value: each]
\end{minted}
respectively.

\section{Bootstrapping $\mu$Kanren}
\label{sec:bootstrapping}

We believe that an unusual development process can be done in Smalltalk and it
is the one that had driven and currently drives the present description.
Instead of creating classes first, we start by looking at some \textit{existing
object} that can be a good candidate to take into account our messages; in
sight of this approach, we give the fundamentals of $\mu$Kanren -- more
generally, for a class of logic programming languages -- starting from the two
nice boolean objects.

\subsection{\texttt{Succeed} and \texttt{Fail} classes}

Let us consider the object \Verb|false| whose responsibility is to represent
\textit{falsehood}, hence it should be a good candidate to provide us something
related to this from the \textit{relational} point of view,
\begin{minted}[fontsize=\footnotesize]{smalltalk}
false asGoal
\end{minted}
namely, \Verb|false| can build for us an object that represents a relation that
cannot be satisfied at all responding to the message selector \Verb|asGoal| --
don't be scared by multiple names, we freely interchange the terms
\textit{goal} and \textit{relation} postponing to later sections the
description of their technical difference. Moreover, let us
assume that the infrastructure that actually builds results is given and implemented
by the block \texttt{B := [:g | g solutions all]}, we write our first test case:
\begin{minted}[fontsize=\footnotesize]{smalltalk}
GoalTest >> testFail
    self
        assert: false asGoal solutions all
        equals: {}
\end{minted}
Now we extend the \Verb|False| class with the following \Verb|asGoal| message
definition
\begin{minted}[fontsize=\footnotesize]{smalltalk}
False >> asGoal
    ^ Goal fail
\end{minted}
and of the message
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Goal >> fail
    ^ Fail new
\end{minted}
such that to say that \verb|Fail| denotes the impossibility to satisfy a relation
is the same to code the message
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Fail >> onState: aState
    ^ Chain bottom
\end{minted}
which will be used within the message \Verb|solutions| sent in the block
\Verb|B| -- a lot remains to be explained, such as the \Verb|Chain| class and
streams in particular, all in what follows.

In parallel, we reproduce for the object \Verb|true| the steps did for
the object \Verb|false|; first,
\begin{minted}[fontsize=\footnotesize]{smalltalk}
GoalTest >> testSucceed
    self
        assert: true asGoal solutions all
        equals: {Var tautology}
\end{minted}
second,
\begin{minted}[fontsize=\footnotesize]{smalltalk}
True >> asGoal
    ^ Goal succeed
\end{minted}
third,
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Goal >> succeed
    ^ Succeed new
\end{minted}
fourth,
\begin{minted}[fontsize=\footnotesize]{smalltalk}
Succeed >> onState: aState
    ^ Chain with: aState
\end{minted}

\section{Theoretical backgrounds}

We quickly review some mathematical backgrounds underlying logic systems;
in particular deductions are performed according to \emph{resolution} methods.

\subsection{Resolution by refutation}

Let $\alpha$ be a sentence in \emph{conjunctive normal
form} (CNF for short) and $M(\alpha)$ the set of models that satisfy $\alpha$
-- recall that a \emph{model} is a set of assignments of values to free
variables in $\alpha$ that make the whole sentence true.  We say that, on one
hand, $\alpha$ is \textit{valid} if it is true in \textit{all} models; on the
other hand, $\alpha$ is \textit{satisfiable} if it is true in \textit{some}
model.

Let $\models$ and $\Rightarrow$ denote the \textit{entail} and \textit{imply}
relations, respectively; so,
\begin{displaymath}
\begin{split}
\alpha \models \beta \leftrightarrow
M(\alpha) \subseteq M(\beta) \leftrightarrow
(\alpha \Rightarrow \beta) \text{ is valid } \leftrightarrow
\neg(\neg\alpha \vee \beta) \text{ is unsatisfiable.}
\end{split}
\end{displaymath}
Therefore, to prove a sentence $\alpha$ reduces to prove
$\neg\alpha\models\perp$, where $\perp$ denotes the empty clause $()$, namely
\textit{falsehood}, which can be deduced by the \textit{resolution rule}
introduced in \citep{Robinson:1965:MLB:321250.321253}; if $l_{i}$ and $m_{r}$
bound to the same variable then
\begin{displaymath}
{\left(l_{0}, \ldots, l_{i}, \ldots, l_{j-1}\right) \quad \left(m_{0}, \ldots, m_{r},\ldots, m_{k-1}\right) \quad l_{i} = \neg m_{r}
\over
\left(l_{0},\ldots, l_{i-1},l_{i+1}, \ldots,l_{j-1}, m_{0},\ldots, m_{r-1},m_{r+1},\ldots, m_{k-1}\right)}
\end{displaymath}
where $\left(l_{0},\ldots, l_{i}, \ldots, l_{j-1}\right) = l_{0}\vee \ldots
\vee l_{i} \vee \ldots \vee l_{j-1}$, for all $l_{q}, m_{w} \in\lbrace 0,1\rbrace$.
This rule is a \textit{complete} inference algorithm, namely it enumerates
\emph{all} deductions for a given sentence, although requiring
\emph{exponential} time; for the sake of concreteness, the \textit{DPLL}
algorithm \citep{Davis:1962:MPT:368273.368557}
is a recursive, depth-first enumeration of models using the
resolution rule paired with heuristics \textit{early termination},
\textit{pure symbol} and \textit{unit clause} to speed up.

\subsection{Resolution by unification}

This method was introduced in \citep{robinson_unif} and
improved by \citep{Martelli:1982:EUA:357162.357169}; it consists of solving
\textit{equations among symbolic expressions}. A \textit{solution} is denoted
as a \textit{substitution} $\theta$, namely a mapping that assigns a symbolic
values to free variables; let $G$ be a set of equations, unification proceeds
according to the following rules:
\begin{description}
\item[delete] $G \cup \lbrace t = t \rbrace \rightarrow G$
\item[decompose] $G \cup \lbrace f(s_{0}, \ldots, s_{k}) = f(t_{0}, \ldots, t_{k})\rbrace$ entails
$$G \cup \lbrace s_{0}=t_{0},\ldots, s_{k}=t_{k} \rbrace$$
\item[conflict] if $f\neq g \vee k\neq m$ then $$G \cup \lbrace f(s_{0}, \ldots, s_{k}) = g(t_{0}, \ldots, t_{m})\rbrace \rightarrow \,\perp$$
\item[eliminate] if $x \not\in vars(t)$ and $x \in vars(G)$ then $$G \cup \lbrace x = t\rbrace \rightarrow G\lbrace x \mapsto t\rbrace \cup \left\lbrace x \triangleq t\right\rbrace $$
\item[occur check] if $x \in vars(f(s_{0},\ldots,s_{k}))$ then $$G \cup \lbrace x = f(s_{0}, \ldots, s_{k})\rbrace \rightarrow \,\perp;$$
without it, generating a $\theta$ is a
\emph{recursive enumerable} problem.
\end{description}

\begin{example}
Let $x$ and $y$ be free variables, the set
$$\lbrace cons(x,cons(x,nil)) = cons(2,y)\rbrace$$
has solution $\theta = \lbrace x \mapsto 2, y \mapsto cons(2,nil) \rbrace$;
on the contrary, the set
$$ \lbrace y = cons(2,y) \rbrace $$
has no \textit{finite} solution, nonetheless
$$\theta = \lbrace y \mapsto cons(2,cons(2,cons(2,...))) \rbrace$$
is a solution upto \textit{bisimulation}
\citep{10.1007/BFb0017309, DBLP:books/daglib/0067019}.
\end{example}
\section{Conclusions and further works}


\begin{itemize}
\item A quine generator is described in \citep{Byrd:2012:MLU:2661103.2661105}
and we aim to reproduce it.

\item using \emph{OCaml} the syntax of the language can be cleanly defined by
algebraic datatypes and the type system helps in the design of the required
operators.  While object-oriented languages make extensibility a relative
simple task, this implementation is harder unless we can make changes to basic
types, at least for what concerns the central \emph{unification} procedure.
All of this is preparatory to modify the tactics mechanism of the \emph{HOL
Light} theorem prover in order to introduce the relational paradigm in that
environment.
\end{itemize}

\bibliographystyle{plainnat}
\bibliography{biblio}
\end{document}
